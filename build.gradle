buildscript {
    repositories {
        maven {
            setUrl("https://arti.tw.ee/artifactory/libs-release")
        }
        mavenLocal()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'com.transferwise.icu'
version '0.3.1'

ext.artifactoryPassword = project.hasProperty("artifactoryPassword") ? project.artifactoryPassword : System.env.ARTIFACTORY_PASSWORD as String
ext.artifactoryUser = project.hasProperty("artifactoryUser") ? project.artifactoryUser : System.env.ARTIFACTORY_USER as String

repositories {
    maven {
        setUrl("https://arti.tw.ee/artifactory/libs-release")
    }
    mavenCentral()
}

dependencies {
    implementation('org.springframework:spring-context:5.2.5.RELEASE') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation 'com.ibm.icu:icu4j:66.1'

    testImplementation 'org.thymeleaf:thymeleaf-spring5:3.0.11.RELEASE'
    testImplementation 'org.thymeleaf:thymeleaf:3.0.11.RELEASE'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'

    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'spring-icu'
            from components.java
            pom {
                name = 'Spring ICU'
                description = 'ICU4j compliant Spring MessageSource'
                url = 'https://github.com/transferwise/spring-icu/'

                developers {
                    developer {
                        id = 'brettanomyces'
                        name = 'Brett Yukich'
                        email = 'brett.yukich@transferwise.com'
                    }
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/transferwise/spring-icu/blob/master/LICENSE'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url "https://arti.tw.ee/artifactory/libs-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}-local"
            credentials {
                username = artifactoryUser
                password = artifactoryPassword
            }
        }
        /*
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
        */
    }
}

/*
signing {
    sign publishing.publications.mavenJava
}
*/

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}


task addTag(type: Exec) {
    commandLine 'git', 'tag', "$project.name-$project.version", '-m', "$project.name-$project.version"
}
task pushTag(type: Exec) {
    commandLine 'git', 'push', 'origin', "$project.name-$project.version"
}

pushTag.dependsOn addTag
